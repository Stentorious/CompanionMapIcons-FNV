int iUITab
int iUITile
int iColor
int iySI
int iRecenter
ref rCompanion
array_var aCompanions
array_var aForE
string_var sUIPath
string_var sIconPath
string_var sName

begin Function {}

	; Process companions and assign icon
	iUITab = GetUIFloatAlt "MapMenu\GLOW_BRANCH\MM_Tabline\_CurrentTab"
	if iUITab < 2
		aCompanions = GetTeammates
		if aCompanions

			; Get icon settings
			iColor = Goo1.AuxVarGetFlt "*CompMapIcons_INI" 0
			iySI = Goo1.AuxVarGetFlt "*CompMapIcons_INI" 1
			iRecenter = Goo1.AuxVarGetFlt "*CompMapIcons_INI" 2

			; Process companions
			foreach aForE <- aCompanions
				rCompanion = *aForE
				sName = rCompanion.LNGetName

				; Icon path varies on Local/World map and interior/exterior companion location
				if iRecenter == 1
					if iUITab == 1
						if eval rCompanion.IsInInterior
							iUITile = (Ar_Size aCompanions) - 1
							while iUITile > -1
								if eval GetUIString ("MapMenu\GLOW_BRANCH\MM_MainRect\MM_WorldMap_ClipWindow\MM_WorldMap_ParentImage\Door:" + $iUITile + "\string") == sName
									sUIPath = sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_WorldMap_ClipWindow\MM_WorldMap_ParentImage\Door :%g" iUITile
									break
								endif
								iUITile -= 1
							loop
						else
							sUIPath = sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_WorldMap_ClipWindow\MM_WorldMap_ParentImage\%z " sName
						endif
					else
						sUIPath = sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\MM_LocalMap_ParentImage\%z " sName
					endif
				else
					if iUITab == 1
						if eval rCompanion.IsInInterior
							iUITile = (Ar_Size aCompanions) - 1
							while iUITile > -1
								if eval GetUIString ("MapMenu\GLOW_BRANCH\MM_MainRect\MM_WorldMap_ClipWindow\MM_WorldMap_ParentImage\Door:" + $iUITile + "\string") == sName
									sUIPath = sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_WorldMap_ClipWindow\MM_WorldMap_ParentImage\Door Marker:%g" iUITile
									break
								endif
								iUITile -= 1
							loop
						else
							sUIPath = sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_WorldMap_ClipWindow\MM_WorldMap_ParentImage\%z Marker" sName
						endif
					else
						sUIPath = sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\MM_LocalMap_ParentImage\%z Marker" sName
					endif
				endif

				; Companion icon needs to be updated
				if eval GetUIFloatAlt (sUIPath + "\_CompanionIcon") == 0

					; Display unconscious icon
					if eval rCompanion.GetKnockedState
						if eval iySI
							sIconPath = "Interface\icons\Aid_DoctorBag.dds"
						else
							sIconPath = "Interface\icons\typeicons\hot_keys_icon_surgerykit.dds"
						endif
						SetUIFloatAlt (sUIPath + "\CompanionIcon\systemcolor") 2

					; Display companion specific icon
					else
						sIconPath = sv_Construct "textures\jazzisparis\ccc\avatar_%z.dds" sName
						if eval FileExists (sIconPath)
							Sv_Replace "textures\|" sIconPath
						else
							sIconPath = sv_Construct "textures\jazzisparis\ccc\avatar_%z.dds" (GetINIString (sv_Construct "Avatar:%z" sName) "Stentorious\CompanionMapIconsDict.ini") ; Set alt CCC path
							if eval FileExists (sIconPath)
								Sv_Replace "textures\|" sIconPath
							elseif eval rCompanion.GetIsSex Male
								sIconPath = "jazzisparis\ccc\avatar_male.dds"
							else
								sIconPath = "jazzisparis\ccc\avatar_female.dds"
							endif
						endif
						if iColor == 1
							SetUIFloatAlt (sUIPath + "\CompanionIcon\systemcolor") 0
						endif
					endif

					; Display icon
					SetUIStringAlt (sUIPath + "\CompanionIcon\filename") (sIconPath)
					SetUIFloatAlt (sUIPath + "\_CompanionIcon") 1

				endif
			loop

			sv_Destruct sUIPath sIconPath sName
			aCompanions = aForE = ar_Null

		endif
	endif
end