; ## Description
; Returns the value of a numeric lStewieAl's Tweaks setting.
; This accounts for any loaded lStewieAl's Tweaks override .INIs
; Caches nvse_stewie_tweaks.ini and all lStewieAl's Tweaks override .INIs if not previously cached:
; - Caching eats memory, but allows faster access to the data.
; - Once the cache is no longer needed, ClearStewieCacheShowOff should be called to free the memory.
; - Utilizes ShowOff xNVSE's cached file data system.

; ## Syntax
; (stewieSetting:float) GetNumericStewieSetting_Cached keyString:string

; ## Example
; float fSetting = Call (CompileScript "_Stentorious\GetNumericStewieSetting_Cached.gek") "Tweaks:bCritChanceIgnoresFireRate"

; ## Notes
; - This function is performance intensive and should be used sparingly.
; - lStewieAl's Tweaks INIs changes made during the current session are not applied until next game launch.

string_var sKeyString

int iSize
array_var aOverrideINIs
string_var sStewie
string_var sTemp

begin Function {sKeyString}

	; Get default setting value
	sStewie = GetINIString_Cached (sKeyString) "..\NVSE\plugins\nvse_stewie_tweaks.ini"

	; Get override INIs setting value
	aOverrideINIs = GetFilesInFolder "NVSE\plugins\Tweaks\INIs" "*.ini"
	if eval (iSize = Ar_Size aOverrideINIs) > 0
		while (iSize -= 1) > -1
			if eval (sTemp = GetINIString_Cached (sKeyString) ("..\NVSE\plugins\Tweaks\INIs\" + (aOverrideINIs[iSize]))) != ""
				sStewie = sTemp
				break
			endif
		loop
	endif

	if eval TestExpr ToNumber sStewie 0 1
		SetFunctionValue #sStewie
	else
		SetFunctionValue -1
	endif

	aOverrideINIs = Ar_Null
	Sv_Destruct sKeyString sStewie sTemp

end